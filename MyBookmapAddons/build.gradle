plugins {
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.6'
}

// Java 17 compatibility for Bookmap
sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

repositories {
    mavenCentral()
    maven {
        url "https://maven.bookmap.com/maven2/releases/"
    }
}

dependencies {
    // Bookmap L1 API dependencies
    compileOnly group: 'com.bookmap.api', name: 'api-core', version: '7.6.0.20'
    compileOnly group: 'com.bookmap.api', name: 'api-simplified', version: '7.6.0.20'
    
    // Apache Commons
    compileOnly group: 'org.apache.commons', name: 'commons-lang3', version: '3.11'
    compileOnly group: 'com.ibm.icu', name: 'icu4j', version: '71.1'
}

jar {
    archiveFileName = 'bm-rithmic-connection-monitor.jar'
}

eclipse.classpath.downloadJavadoc = true
idea.module.downloadJavadoc = true

if(idea.project) {
    // Create 'BookmapJar' run configuration for IntelliJ
    idea.project.settings.runConfigurations {
        def addOpensOptions = '--add-opens=java.base/java.lang=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.io=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.math=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.util=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.util.concurrent=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.net=ALL-UNNAMED\n' +
                '--add-opens=java.base/java.text=ALL-UNNAMED\n' +
                '--add-opens=java.desktop/java.awt=ALL-UNNAMED\n' +
                '--add-opens=java.desktop/java.awt.color=ALL-UNNAMED\n' +
                '--add-opens=java.desktop/java.awt.peer=ALL-UNNAMED\n' +
                '--add-opens=java.desktop/com.sun.java.swing=ALL-UNNAMED\n' +
                '--add-opens=java.prefs/java.util.prefs=ALL-UNNAMED'
        if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
            addOpensOptions += '--add-opens=java.desktop/sun.awt.windows=ALL-UNNAMED\n'
        }

        'BookmapJar'(org.jetbrains.gradle.ext.JarApplication) {
            // Change the path to Bookmap.jar here if you changed the default installation directory
            jarPath = 'C:\\Program Files\\Bookmap\\Bookmap.jar'
            workingDirectory = 'C:\\Bookmap'
            jvmArgs = addOpensOptions
        }
    }
} 